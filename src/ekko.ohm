Ekko {
  Program   = Actor+
  Actor     = actor id Locals? Block
  Locals    = "[" ListOf<id, ","> "]"
  Block     = "{" Stmt+"}"
  Stmt      = id "=" Exp ";"                            -- assign
            | print Exp ";"                             -- print
            | while Exp Block                           -- while
            | loop (Exp times)? Block                   -- for
            | if Exp Block (else (Stmt_if | Block))?    -- if
            | send Exp to id ";"                        -- send
            | receive id ";"                            -- receive
            | back num ";"                              -- back
            | forward num ";"                           -- forward
            | commit ";"                                -- commit
            | abort ";"                                 -- abort
  Exp       = ("-" | "!") Exp7                          -- unary
            | Exp1 "?" Exp1 ":" Exp                     -- ternary
            | Exp1
  Exp1      = Exp1 "||" Exp2                            -- binary
            | Exp2
  Exp2      = Exp2 "&&" Exp3                            -- binary
            | Exp3
  Exp3      = Exp4 ("<="|"<"|"=="|"!="|">="|">") Exp4   -- binary
            | Exp4
  Exp4      = Exp4 ("+" | "-") Exp5                     -- binary
            | Exp5
  Exp5      = Exp5 ("*" | "/" | "%") Exp6               -- binary
            | Exp6
  Exp6      = Exp7 "**" Exp6                            -- binary
            | Exp7
  Exp7      = num
            | true
            | false
            | nil
            | id                                        -- id
            | "(" Exp ")"                               -- parens

  actor   = "actor" ~idchar
  print     = "print" ~idchar
  while     = "while" ~idchar
  loop      = "loop" ~idchar
  times     = "times" ~idchar
  if        = "if" ~idchar
  else      = "else" ~idchar
  send      = "send" ~idchar
  to        = "to" ~idchar
  receive   = "receive" ~idchar
  back      = "back" ~idchar
  forward   = "forward" ~idchar
  commit    = "commit" ~idchar
  abort     = "abort" ~idchar
  true      = "true" ~idchar
  false     = "false" ~idchar
  nil       = "nil" ~idchar
  keyword   = actor | print | while | loop | times | if | else
            | send | to | receive | back | forward | commit | abort
            | true | false | nil
  num       = digit+ ("." digit+)? (("E" | "e") ("+" | "-")? digit+)?
  id        = ~keyword letter idchar*
  idchar    = letter | digit | "_"
  space    += "//" (~"\n" any)*                         -- comment
}
