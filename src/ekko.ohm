Ekko {
  Program   = Process+
  Process   = process id Locals? do Block
  Locals    = "[" ListOf<id, ","> "]"
  Block     = Statement* endkw
  Statement = id "=" Exp ";"                            -- assign
            | print Exp ";"                             -- print
            | while Exp do Block                        -- while
            | loop Exp times do Block                   -- for
            | IfStmt
            | send Exp to id ";"                        -- send
            | receive id from id ";"                    -- receive
            | commit ";"                                -- commit
            | back num ";"                              -- back
  IfStmt    = if Exp then Block else Block              -- else
            | if Exp then Block else IfStmt             -- elseif
            | if Exp then Block                         -- if
  Exp       = ("-" | "!") Exp7                          -- unary
            | Exp1 "?" Exp1 ":" Exp                     -- ternary
            | Exp1
  Exp1      = Exp1 "||" Exp2                            -- binary
            | Exp2
  Exp2      = Exp2 "&&" Exp3                            -- binary
            | Exp3
  Exp3      = Exp4 ("<="|"<"|"=="|"!="|">="|">") Exp4   -- binary
            | Exp4
  Exp4      = Exp4 ("+" | "-") Exp5                     -- binary
            | Exp5
  Exp5      = Exp5 ("*" | "/" | "%") Exp6               -- binary
            | Exp6
  Exp6      = Exp7 "**" Exp6                            -- binary
            | Exp7
  Exp7      = num
            | true
            | false
            | id                                        -- id
            | "(" Exp ")"                               -- parens

  process   = "process" ~idchar
  print     = "print" ~idchar
  while     = "while" ~idchar
  loop      = "loop" ~idchar
  times     = "times" ~idchar
  if        = "if" ~idchar
  then      = "then" ~idchar
  else      = "else" ~idchar
  do        = "do" ~idchar
  endkw     = "end" ~idchar
  send      = "send" ~idchar
  receive   = "receive" ~idchar
  to        = "to" ~idchar
  from      = "from" ~idchar
  back      = "back" ~idchar
  commit    = "commit" ~idchar
  true      = "true" ~idchar
  false     = "false" ~idchar
  keyword   = process | print | while | loop | times | if | then | else
            | do | endkw | send | receive | to | from | back | commit
            | true | false
  num       = digit+ ("." digit+)? (("E" | "e") ("+" | "-")? digit+)?
  id        = ~keyword letter idchar*
  idchar    = letter | digit | "_"
  space    += "//" (~"\n" any)*                         -- comment
}
